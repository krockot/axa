import("//build/config/android/rules.gni")

group("client") {
  deps = [ ":axa_client_apk" ]
}

android_apk("axa_client_apk") {
  apk_name = "AxaClient"
  android_manifest = "AndroidManifest.xml"
  deps = [
    ":native_library",
    ":src_java",
  ]
  shared_libraries = [ ":native_library" ]
}

android_resources("res_java") {
  android_manifest = "AndroidManifest.xml"
  resource_dirs = [ "//axa/client/res" ]
  deps = [
    "//third_party/android_deps:com_android_support_appcompat_v7_java",
  ]
}

android_library("src_java") {
  java_files = [
    "src/org/chromium/axa_client/MainActivity.java",
    "src/org/chromium/axa_client/NativeBridge.java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  deps = [
    ":native_library",
    ":res_java",
    "//base:base_java",
    "//base:jni_java",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "src/org/chromium/axa_client/NativeBridge.java",
  ]

  deps = [
    "//base:base_java",
  ]
}

shared_library("native_library") {
  output_name = "axa_client_native"
  sources = [
    "native_bridge.cc",
  ]

  deps = [
    ":jni_headers",
    "//axa/mojom",
    "//base",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
  ]

  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

